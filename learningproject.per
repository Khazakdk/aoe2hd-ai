;Author: Joe Sommer
;This AI is being built as a learning exercise, following along with BearTheGreat's training example


; make sure to add age up rules
(defrule
   (can-research feudal-age)
   =>
   (research feudal-age)
)

(defrule
   (can-research castle-age)
   =>
   (research castle-age)
)

(defrule
   (can-research imperial-age)
   =>
   (research imperial-age)
)
; end age up rules

; train villagers and build houses, basic stuff we do all game
(defrule
   (unit-type-count-total villager < 100)
   (can-train villager)
   =>
   (train villager)
)

(defrule
   (housing-headroom < 4)
   (population-headroom > 3)
   (can-build house)
   =>
   (build house)
)
; end basic

;The default strategic numbers are not working for us.  Let's force no civilian
;explorers after the berries are found.  We still want our scout to explore.


(defrule
   (resource-found food)
   =>
   (chat-local-to-self "found food")
   (set-strategic-number sn-percent-civilian-explorers 0) ; default 34
   (set-strategic-number sn-cap-civilian-explorers 0) ;default 2
   (set-strategic-number sn-number-explore-groups 1) ; default 0, should let our scout go scout
   (set-strategic-number sn-maximum-explore-group-size 1) ; default 3
   (set-strategic-number sn-initial-exploration-required 0) ; default 100
   (disable-self)
)

;So far so good.  We need 2 Dark Age buildings and 500 food to advance to the
;Feudal Age.  There are absolutely refinements that can be made to the
;following rule, but for now let's keep it simple.  Let's say we want 60% of
;our villagers on food and 40% on wood.

(defrule
   (current-age == dark-age)
   =>
   (chat-local-to-self "starting dark age allocations")
   (set-strategic-number sn-food-gatherer-percentage 60)
   (set-strategic-number sn-wood-gatherer-percentage 40)
   (set-strategic-number sn-gold-gatherer-percentage 0)
   (set-strategic-number sn-stone-gatherer-percentage 0)
)

(defrule
   (building-type-count-total house > 0) ;Start first house before mill.
   (building-type-count-total mill < 1)
   (resource-found food)
   (can-build mill)
   =>
   (chat-local-to-self "building mill")
   (build mill)
)

; for now I am skipping the training ai's rules for placing lumber camps and just building one
(defrule
   (current-age == dark-age)
   (building-type-count-total mill > 0)
   (resource-found wood)
   (building-type-count-total lumber-camp < 1)
   (can-build lumber-camp)
   =>
   (build lumber-camp)
)

(defrule
   (current-age == dark-age)
   (building-type-count-total mill > 0)
   (building-type-count-total lumber-camp > 0)
   (or(building-type-count-total farm < 15)
      (idle-farm-count == 0))
   (can-build farm)
   =>
   (build farm)
)